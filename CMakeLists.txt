PROJECT(TyphonIO C)

CMAKE_MINIMUM_REQUIRED(VERSION 2.8.9)

SET(TYPHONIO_MAJOR_VERSION 1)
SET(TYPHONIO_MINOR_VERSION 6)

SET(TYPHONIO_VERSION "${TYPHONIO_MAJOR_VERSION}.${TYPHONIO_MINOR_VERSION}")

SET(CMAKE_POSITION_INDEPENDENT_CODE ON)

SET(CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake")

OPTION(BUILD_TIO_SHARED "BUILD SHARED LIBRARY" OFF)
OPTION(BUILD_FORTRAN_LIBRARY "BUILD_FORTRAN_LIBRARY" OFF)

OPTION(BUILD_DOXYGEN_DOCS "BUILD DOXYGEN DOCUMENTATION" OFF)
OPTION(BUILD_USER_GUIDE "BUILD USER GUIDE DOCUMENATION"	ON)

# Enable Package Target to be build with CPack
INCLUDE(CPack)

## MPI
FIND_PACKAGE(MPI REQUIRED)
LIST(APPEND TIO_EXTERNAL_LIBS ${MPI_C_LIBRARIES})
INCLUDE_DIRECTORIES(${MPI_C_INCLUDE_PATH})
## HDF5 
FIND_PACKAGE(HDF5 COMPONENTS C HL REQUIRED)
LIST(APPEND TIO_EXTERNAL_LIBS ${HDF5_C_LIBRARIES})
INCLUDE_DIRECTORIES(${HDF5_INCLUDE_DIR})
## Doxygen
IF(BUILD_DOXYGEN_DOCS)
	FIND_PACKAGE(Doxygen)
ENDIF(BUILD_DOXYGEN_DOCS)

# Src Subdirectory
INCLUDE_DIRECTORIES(src)
ADD_SUBDIRECTORY(src)

# Fortran Subdirectory
IF(BUILD_FORTRAN_LIBRARY)
	ENABLE_LANGUAGE(Fortran)
	INCLUDE(${CMAKE_MODULE_PATH}/SetFortranFlags.cmake)
	INCLUDE_DIRECTORIES(src/F90 F2C)
	ADD_SUBDIRECTORY(src/F90)
ENDIF(BUILD_FORTRAN_LIBRARY)

# Doxygen Generated Documentation
IF(DOXYGEN_FOUND)
	IF(DOXYGEN_DOT_EXECUTABLE)
		SET(FOUND_DOT "YES")
	ELSE(DOXYGEN_DOT_EXECUTABLE)
		SET(FOUND_DOT "NO")
	ENDIF(DOXYGEN_DOT_EXECUTABLE)

	SET(DOXYGEN_IN ${CMAKE_CURRENT_SOURCE_DIR}/docs/Doxyfile.in)
	SET(DOXYGEN_OUT ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile)

	CONFIGURE_FILE(${DOXYGEN_IN} ${DOXYGEN_OUT} @ONLY)

	ADD_CUSTOM_TARGET(doxygen_docs ALL
		COMMAND ${DOXYGEN_EXECUTABLE} ${DOXYGEN_OUT}
		WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
		COMMENT "Generating API documentation with Doxygen"
		VERBATIM
	)
	INSTALL(DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/docs/html/ DESTINATION ${CMAKE_INSTALL_PREFIX}/share/doxy_docs)
ENDIF(DOXYGEN_FOUND)

# HTML User Guide Documentation
IF(BUILD_USER_GUIDE)
	INSTALL(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/docs/user_guide DESTINATION ${CMAKE_INSTALL_PREFIX}/share)
ENDIF(BUILD_USER_GUIDE)